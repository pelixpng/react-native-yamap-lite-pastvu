buildscript {
  ext.getExtOrDefault = { name ->
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['YamapLite_' + name]
  }

  ext {
    minSdkVersion = 26
    compileSdkVersion = 34
  }

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:8.7.2"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('kotlinVersion')}"
  }
}

apply plugin: "com.android.library"
apply plugin: "org.jetbrains.kotlin.android"

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name)
    ? rootProject.ext.get(name)
    : (project.properties["YamapLite_" + name]).toInteger()
}

android {
  namespace = "com.yamaplite"
  compileSdk = getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdk = getExtOrIntegerDefault("minSdkVersion")
    targetSdk = getExtOrIntegerDefault("targetSdkVersion")
  }

  buildFeatures {
    buildConfig = true
  }

  buildTypes {
    release {
      minifyEnabled = false
    }
  }

  lint {
    disable 'GradleCompatible'
  }

  compileOptions {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
  }

  kotlinOptions {
    jvmTarget = "17"
  }

  sourceSets {
    main {
      java.srcDirs += [
        "app/build/generated/source/codegen/java",
        "build/generated/source/codegen/java",
        "generated/java",
        "generated/jni"
      ]
    }
  }
}

repositories {
  google()
  mavenCentral()
}

def kotlin_version = getExtOrDefault("kotlinVersion")

dependencies {
  implementation "com.facebook.react:react-android:0.79.2"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  implementation 'com.yandex.android:maps.mobile:4.24.0-lite'
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
}

afterEvaluate {
  def react = rootProject.ext.has("react") ? rootProject.ext.get("react") : null
  if (react != null && react.hasProperty("registerReactNativeModule")) {
    react.registerReactNativeModule(project)
  }
}
